plugins {
    id 'idea'
    id 'java'
    id "java-library"

    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
    mavenCentral()
}

dependencies {
    ['annotationProcessor', 'compileOnly', 'testAnnotationProcessor', 'testCompileOnly'].each {
        config -> add(config, 'org.projectlombok:lombok:1.18.22')
    }

    implementation 'org.apache.commons:commons-lang3:3.12.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

group = 'com.github.eltonsandre.utils'
version = '1.0.1'
description = 'Util para ofuscar dados de objetos'

java.sourceCompatibility = JavaVersion.VERSION_1_8
java.targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:unchecked", "-parameters"]
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

wrapper() {
    gradleVersion = '7.3.1'
    distributionUrl = distributionUrl.replace('bin', 'all')
}